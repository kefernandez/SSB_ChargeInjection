* Created in LTspice Version XVII                               *
*                                            					*
* GaN Systems Inc. Power Transistors                            *
* LTSpice Library for GaN Transistors                           *
* Version 4.1                                                   *
*                                                               *
*****************************************************************
*****************************************************************
*                                                               *
* Models provided by GaN Systems Inc. are not warranted by      *
* GaN Systems Inc. as                                           *
* fully representing all of the specifications and operating    *
* characteristics of the semiconductor product to which the     *
* model relates. The model describe the characteristics of a    *
* typical device.                                               *
* In all cases, the current data sheet information for a given  *
* device is the final design guideline and the only actual      *
* performance specification.                                    *
* Altough models can be a useful tool in evaluating device      *
* performance, they cannot model exact device performance under *
* all conditions, nor are they intended to replace bread-       *
* boarding for final verification. GaN Systems Inc. therefore   *
* does not assume any liability arising from their use.         *
* GaN Systems Inc. reserves the right to change models without  *
* prior notice.                                                 *
*                                                               *
* This library contains models of the following GaN Systems     *
* Inc. transistors:                                             *
*                                                               *
*   GS66516T                                                    *
*****************************************************************
*Level 3. In addition to Level 2, this level includes the stray inductance in the package.
*$
*$
.subckt GaN_LTspice_GS66516T_L3V4P1 gatein drainin sourcein TC TJ
*
*
.param rTC=-0.004  gan_res={22.4e-3}		metal_res={2.0e-3}   gtc=2.85 sh_s = 0.05263   sh_d = 0.94376
.param  cur=.105   x0_0=1.1	  x0_1=1.1  x0_2=1.0  thr = 1.61  itc=0.798 atc=174.1
*
*
Cth_1 0 TJ {1.4e-4}
Cth_2 0 T11  {(1.23e-3)}
Cth_3 0 T22 {(10.8e-3)}
Cth_4 0 T33 {(3.3e-3)}
*
*
Rth_1 T11 TJ {0.01}
Rth_2 T22 T11 {0.14}
Rth_3 T33 T22 {0.14}
Rth_4 TC T33 {0.01}
*
*
bdtemp 0 TJ I = (if(v(drain,source)>0,
+   (cur*(-(v(TJ)-25)*itc+atc)*log(1.0+exp(26*(v(gate,source)-thr)))*
+	v(drain,source)/(1 + max(x0_0+x0_1*(v(gate,source)+x0_2),0.2)*v(drain,source)))*
+	v(drainin,sourcein),
+   (cur*(-(v(TJ)-25)*itc+atc)*log(1.0+exp(26*(v(gate,drain)-thr)))*
+	v(source, drain)/(1 + max(x0_0+x0_1*(v(gate,drain)+x0_2),0.2)*v(source,drain)))*
+	v(sourcein, drainin)))
*
*
ld drainin drain3 {370e-12} Rser=0
ERES_d drain3 3_d value={I(VSENSE_d)*sh_d* (metal_res*(1-1*rTc*(V(TJ)-25)) + gan_res*PWR((v(TJ)+273)/298,gtc)) }
VSENSE_d 3_d drain DC 0
*
*
VSENSE_s 3_s source DC 0
ERES_s source3 3_s value={I(VSENSE_s)* sh_s * (metal_res*(1-1*rTc*(V(TJ)-25)) + gan_res*PWR((v(TJ)+273)/298,gtc)) }
Ls sourcein source3 {150e-12} Rser=0
*
*
rg gatein gate1 {0.5}
Lg gate1 gate {3.1e-9} Rser=0
*
*
Rcsdconv drain source {4000Meg}
Rcgsconv gate source {4000Meg}
Rcgdconv gate drain {4000Meg}
*
*
bswitch drain2 source2 I = (if(v(drain2,source2)>0,
+   (cur*(-(v(TJ)-25)*itc+atc)*log(1.0+exp(26*(v(gate,source2)-thr)))*
+	v(drain2,source2)/(1 + max(x0_0+x0_1*(v(gate,source2)+x0_2),0.2)*v(drain2,source2))),

+   (-cur*(-(v(TJ)-25)*itc+atc)*log(1.0+exp(26*(v(gate,drain2)-thr)))*
+	v(source2, drain2)/(1 + max(x0_0+x0_1*(v(gate,drain2)+x0_2),0.2)*v(source2,drain2)))))
*
*
R_drain2 drain2 drain {(1e-4)}
R_source2 source2 source {(1e-4)}
*
*
C_GS	 gate source  	{372.3e-12}
C_GS1 gate source Q = ( - 57.24e-12*(1-1./(1+exp(0.477*(-v(drain, source)+3.0))))
+                       - 86.64e-12*(1-1./(1+exp(0.067*(-v(drain, source)+32.3))))
+                         - 5.0e-12*(1-1./(1+exp(0.5*(-v(drain, source)+20.3))))
+                        -2.124e-12*(1-1./(1+exp(0.0177*(-v(drain, source)+600.03))))

+					 - 5.19e-11*(-1+1./(1+exp(0.06*(-v(drain, source)+0.0))))
+				     - 0.88e-10*(-1+1./(1+exp(0.4*(-v(drain, source)+0.4))))
+    				 - 0.54e-11*(-1+1./(1+exp(0.2*(-v(drain, source)+35.1))))
+    				 - 0.96e-11*(-1+1./(1+exp(0.0001*(-v(drain, source)+70.1)))) )*x
*
*
C_GS2 gate source   Q = ( 1.43e-010*log(1+exp(0.1*(x-0.5)))
+				      + 1.73e-010*log(1+exp(7.5*(x-0.55)))
+			          - 2.03e-10*log(1+exp(0.7*(-x+4))) )
*
*
C_GD   gate drain  	{3.6e-012}
C_GD1  gate drain Q = ( 1.2e-10*log(1+exp(0.477*(x+3)))+
+					 15.2e-10*log(1+exp(0.0670*(x+32.3)))+
+					  0.1e-10*log(1+exp(0.50*(x+20.3)))+
+					  1.2e-10*log(1+exp(0.0177*(x+600.03))) )
*
*
C_SD	 source drain  	{1.23e-010}
C_SD1	source drain Q = ( 3e-10*log(1+exp(.3*(x+0.01)))+
+						5.31e-09*log(1+exp(.14*(x+66)))+
+						 2.79e-9*log(1+exp(.06*(x+190)))+
+						8.66e-10*log(1+exp(.041*(x+300))) )
*
*
.ends
*$
